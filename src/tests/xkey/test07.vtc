varnishtest "Test xkey vmod multiple objects purging multiple keys"

server s1 {
	rxreq
	txresp -hdr "xkey: 1000  2000	3000"
	rxreq
	txresp -hdr "xkey: 4000	   5000 6000"
	rxreq
	txresp -hdr "xkey:  7000  8000  9000"
} -start

varnish v1 -vcl+backend {
	import xkey from "${vmod_builddir}/.libs/libvmod_xkey.so";

	sub vcl_recv {
		if (req.http.xkey-purge) {
			if (xkey.purge(req.http.xkey-purge) != 0) {
				return (synth(200, "Purged"));
			} else {
				return (synth(404, "No key"));
			}
		}
	}

	sub vcl_backend_response {
		set beresp.ttl = 60s;
		set beresp.grace = 0s;
		set beresp.keep = 0s;
	}

	sub vcl_synth {
		set resp.http.reason = resp.reason;
	}
} -start

client c1 {
	txreq -url "/one"
	rxresp
	txreq -url "/two"
	rxresp
	txreq -url "/three"
	rxresp
} -run

varnish v1 -expect n_object == 3

client c1 {
	txreq -hdr "xkey-purge:	1000  5000	9000"
	rxresp
	expect resp.status == 200
	expect resp.http.reason == "Purged"
} -run

delay 1

varnish v1 -expect n_object == 0
