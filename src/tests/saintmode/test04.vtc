varnishtest "varnish-modules issue #27"
# Check health of objects

server s1 {
	rxreq
	expect req.url == "/a"
	txresp -hdr "foo: 1"

	rxreq
	expect req.url == "/a"
	txresp -hdr "Saint: yes"

	rxreq
	expect req.url == "/b"
	txresp

	rxreq
	expect req.url == "/c"
	txresp
} -start

varnish v1 -vcl+backend {
	import saintmode from "${vmod_builddir}/.libs/libvmod_saintmode.so";
	import directors;

	sub vcl_init {
		new sm = saintmode.saintmode(s1, 3);
	}

	sub vcl_backend_fetch {
		set bereq.backend = sm.backend();
	}

	sub vcl_backend_response {
		if (beresp.http.Saint == "yes") {
			saintmode.blacklist(5s);
			return (retry); # -> 503
		} else {
			set beresp.ttl = 0.1s;
			set beresp.grace = 10s;
		}
	}

	sub vcl_hit {
		if (!sm.is_healthy()) {
			return(synth(466, "object is sick!"));
		}
	}

	# special case: vcl_hash and vcl_recv does not have digest
	sub vcl_recv {
		if (req.url == "/c" && !sm.is_healthy()) {
			return (pass);
		}
	}

} -start

client c1 {

	# first
	txreq -url "/a"
	rxresp
	expect resp.status == 200
	expect resp.http.foo == 1

	delay 0.2

	# same, from cache
	txreq -url "/a"
	rxresp
	expect resp.status == 200
	expect resp.http.foo == "1"

	delay 0.5

	# TTL expired, grace is not
	txreq -url "/a"
	rxresp
	expect resp.status == 466

	# the backend is still overall healthy
	txreq -url "/b"
	rxresp
	expect resp.status == 200

	txreq -url "/c"
	rxresp
	expect resp.status == 200
}

client c1 -run
